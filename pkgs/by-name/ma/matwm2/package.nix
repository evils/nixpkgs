{ lib, stdenv
, fetchFromGitHub
, pkg-config
, libX11
, which
, shapedWindows ? true, libXext
, xineramaSupport ? true, libXinerama
, xftSupport ? true, libXft
, debug ? false }:

stdenv.mkDerivation rec {

  pname = "matwm2";
  version = "git-0.1.2pre3";

  src = fetchFromGitHub {
    owner = "segin";
    repo = "${pname}";
    rev = "5d1c7f8838b29b525ebae612d986c0b6fb04f7a3";
    sha256 = "0fvjrjg99zhjd3kbi1xr1p7w7kmqb1rz8w9p6ib2cb7ngw5m7i1w";
  };

  preConfigure = ''
    substituteInPlace matwm2.1	 --replace "System wide configuration file." "System wide configuration file generated by nix."
  '';

  configureFlags = [
    "--mandir=${placeholder "out"}/share/man"
    "--prefix=${placeholder "out"}" ]
    ++ lib.optional debug "--enable-debug";

  nativeBuildInputs = [ pkg-config which ];

  buildInputs = [ libX11 ]
    ++ lib.optional shapedWindows libXext
    ++ lib.optional xineramaSupport libXinerama
    ++ lib.optional xftSupport libXft;

  dontStrip = debug;

  meta = with lib; {
    description = "A simplistic overlapping X11 window manager";
    homepage = "https://github.com/segin/matwm2";
    license = licenses.mit;
    platforms = platforms.all;
    maintainers = with maintainers; [ evils ];
    longDescription = ''
      matwm2 is a simplistic overlapping X11 window manager.
      It features window frames with titlebar and buttons, configurable keybindings and mouse buttons, support for EWMH and motif hints, focus-follows-mouse and click-to-focus focus models, virtual desktops, Xft fonts and xinerama support.
    '';
  };
}
